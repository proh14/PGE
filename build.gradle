plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.26.0'
}

group 'me.proh14'
version '1.0.0'
description 'PGE is an Encryptor program that stands for Pretty Good Encryptor'

repositories {
    mavenCentral()
    jcenter()
}

ext {
    junitVersion = '5.9.0'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule.set('me.proh14.pge')
    mainClass = 'me.proh14.pge.Main'
}

javafx {
    version = '17.0.6'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    // https://mvnrepository.com/artifact/org.beryx.jlink/org.beryx.jlink.gradle.plugin
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/${project.name}-${project.version}-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }

    jpackage {
        installerName = project.name
        imageName = project.name
        vendor = 'Proh14'
        installerOptions = [
                '--description', project.description,
                '--copyright', 'Copyrigth Â© 2023',
                '--app-version', project.version
        ]

        installerType = project.findProperty('installerType')
        // we will pass this from the command line (example: -PinstallerType=msi)
        if (installerType == 'msi') {
            imageOptions += ['--icon', 'src/main/resources/me/proh14/pge/Images/ApplicationIcons/50x50.ico']
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser',
                    '--win-menu', '--win-shortcut'
            ]
        }
        if (installerType == 'pkg') {
            imageOptions += ['--icon', 'src/main/resources/me/proh14/pge/Images/ApplicationIcons/50x50.ico']
        }
        if (['deb', 'rpm'].contains(installerType)) {
            imageOptions += ['--icon', 'src/main/resources/me/proh14/pge/Images/ApplicationIcons/50x50.png']
            installerOptions += [
                    '--linux-menu-group', 'Educational',
                    '--linux-shortcut'
            ]
        }
        if (installerType == 'deb') {
            installerOptions += [
                    '--linux-deb-maintainer', 'test@gmail.com'
            ]
        }
        if (installerType == 'rpm') {
            installerOptions += [
                    '--linux-rpm-license-type', 'GPL-3.0'
            ]
        }
    }
}

jlinkZip {
    group = 'distribution'
}

jpackage {
    doFirst {
        project.getProperty('installerType') // throws exception if its missing
    }
}

